buildscript {
    apply from: 'versions.gradle'

    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        mavenCentral()
        jcenter()
    }

     dependencies {
         classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.0"
    id "com.moowork.node" version "1.2.0"
}

allprojects {
    group 'io.github.kmulti'
    version '1.2.41.1'

    repositories {
        mavenCentral()
    }

}

apply plugin: "kotlin"

subprojects {
    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    afterEvaluate {
        task sourceJar(type: Jar, dependsOn: classes) {
            classifier 'sources'
            from sourceSets.main.kotlin
            duplicatesStrategy = "exclude"
            def platformSrc = sourceSets.main.kotlin
            def commonSrc = project(':kmulti-test-common').sourceSets.main.kotlin
            from (platformSrc + commonSrc)
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }
    }

    def varintName = "${project.name}"
    tasks.withType(Jar) {
        archivesBaseName = varintName
    }

    // Create the pom configuration:
    def pomConfig = {
        name "kmulti-test"
        description "kmulti-test $version - Kotlin multiplatform testing utilities"
        url "https://github.com/kmulti/kmulti-test"

        scm {
            url "https://github.com/kmulti/kmulti-test/tree/master"
            connection "scm:git:git://github.com/kmulti/kmulti-test.git"
            developerConnection "scm:git:ssh://github.com:kmulti/kmulti-test.git"
        }
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                name "Marcin Moskala"
            }

            developer {
                id "abissell"
                name "Andrew Bissell"
                email "abissell@gmail.com"
            }
        }
    }

    publishing {
        publications {
            mavenProject(MavenPublication) {
                from components.java
                groupId project.group
                artifactId varintName
                version project.version

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', "kmulti-test $version - Kotlin multiplatform testing utilities")
                    root.children().last() + pomConfig
                }

                artifact sourceJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
    }

    bintray {
        user = System.getProperty('bintray.user')
        key = System.getProperty('bintray.key')
        publications = ['mavenProject']
        pkg {
            repo = 'kmulti-test'
            name = 'kmulti-test'
            userOrg = 'kmulti'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/kmulti/kmulti-test'
            websiteUrl = 'https://github.com/kmulti/kmulti-test'
            issueTrackerUrl = 'https://github.com/kmulti/kmulti-test/issues'

            githubRepo = 'kmulti/kmulti-test'
            version {
                name = project.version
                desc = "kmulti-test - Kotlin multiplatform testing utilities"
                released = new Date()
            }
        }
    }
}
